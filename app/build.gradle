/*
 * Copyright (C) 2019 by onlymash <im@fiepi.me>, All rights reserved
 *
 * This program is free software: you can redistribute it and/or modify it under
 * the terms of the GNU General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program. If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

Properties properties = new Properties()
properties.load(project.rootProject.file('.gradle/keystore.properties').newDataInputStream())

Integer verCode = 'git rev-list HEAD --first-parent --count'.execute().text.trim().toInteger()
String verNameSuffix = '.' + 'git rev-parse --short HEAD'.execute().text.trim()

android {
    signingConfigs {
        github {
            storeFile file('../.gradle/flexbooru.jks')
            keyAlias properties.getProperty('KEY_ALIAS')
            keyPassword properties.getProperty('KEY_PASS')
            storePassword properties.getProperty('STORE_PASS')
        }
        play {
            storeFile file('../.gradle/flexbooru_play.jks')
            keyAlias properties.getProperty('KEY_ALIAS')
            keyPassword properties.getProperty('KEY_PASS')
            storePassword properties.getProperty('STORE_PASS')
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "onlymash.flexbooru"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode verCode
        versionName '0.7.7'
        versionNameSuffix verNameSuffix
        multiDexEnabled true
        resConfigs 'en', 'zh-rCN', 'zh-rHK', 'zh-rTW', 'nl-rNL', 'pt-rBR', 'es-rES', 'pl-rPL', 'fr-rFR'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "flexbooru_${defaultConfig.versionName}.apk"
        }
    }
    flavorDimensions 'github', 'play'
    productFlavors {
        github {
            dimension 'github'
        }
        play {
            dimension 'play'
            applicationIdSuffix '.play'
            versionNameSuffix '.play'
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            productFlavors.github.signingConfig signingConfigs.github
            productFlavors.play.signingConfig signingConfigs.play
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kapt {
        useBuildCache = true
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    lintOptions {
        abortOnError true
    }
//    packagingOptions {
//        exclude 'META-INF/library-core_release.kotlin_module'
//    }
}

File jsonFile = file('update.json')
def builder = new JsonBuilder(new JsonSlurper().parseText(jsonFile.text))
builder.content.version_code = android.defaultConfig.versionCode
builder.content.version_name = android.defaultConfig.versionName + android.defaultConfig.versionNameSuffix
jsonFile.write(builder.toPrettyString())

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //Room requires
    implementation "org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.0.5"
    implementation 'androidx.annotation:annotation:1.1.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'androidx.core:core-ktx:1.1.0-alpha05'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha04'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.browser:browser:1.0.0'
    implementation "androidx.room:room-runtime:$room"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha4'
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle"
    implementation 'androidx.paging:paging-runtime-ktx:2.1.0'
    implementation 'android.arch.work:work-runtime-ktx:1.0.1'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'com.google.android.apps.muzei:muzei-api:3.1.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.mikepenz:materialdrawer:6.1.2'
    implementation 'com.github.kenglxn.QRGen:android:2.5.0'
    implementation 'xyz.belvi.mobilevision:barcodescanner:2.0.3'
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    implementation 'com.google.android.gms:play-services-vision:17.0.2'
    implementation 'androidx.preference:preference-ktx:1.1.0-alpha04'
    implementation 'com.takisoft.preferencex:preferencex-simplemenu:1.0.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
    implementation 'com.squareup.okio:okio:2.3.0-SNAPSHOT'
    implementation 'com.squareup.picasso:picasso:3.0.0-SNAPSHOT'
    implementation "com.github.bumptech.glide:glide:$glide"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer"
    implementation "com.tickaroo.tikxml:annotation:$tikxml"
    implementation "com.tickaroo.tikxml:core:$tikxml"
    implementation "com.tickaroo.tikxml:retrofit-converter:$tikxml"
    kapt "com.tickaroo.tikxml:processor:$tikxml"
    kapt "androidx.room:room-compiler:$room"
    kapt "com.github.bumptech.glide:compiler:$glide"
    testImplementation 'junit:junit:5.0-SNAPSHOT'
    androidTestImplementation 'androidx.test:runner:1.2.0-alpha04'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha04'
}

apply plugin: 'com.google.gms.google-services'
